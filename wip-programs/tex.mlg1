#width 200
#height 100
#tickrate 60
#memory 0

$FONT "wip-programs/assets/8x8font.png"

fn draw_texture(address, x, y) {
    let img_width = get(address)
    let img_height = get(address+1)
    let img_area = img_width * img_height

    address = address + 2
    let i = 0
    while i < img_area {
        let pixel_data = get(address+i)
        let r = pixel_data % 256
        pixel_data = pixel_data / 256
        let g = pixel_data % 256
        pixel_data = pixel_data / 256
        let b = pixel_data % 256

        color(r, g, b)
        let px = i % img_width + x
        let py = i / img_height + y
        point(px, py)

        i = i + 1
    }
}


fn draw_texture_region(address, x, y, rx, ry, rwidth, rheight) {
    let img_width = get(address)

    let pixel_pointer = address + 2 + ry * img_width + rx

    let region_area = rwidth * rheight
    let width_minus_rwidth = img_width - rwidth

    let i = 0
    while i < region_area {
        if !(i == 0) * (i % rwidth == 0) {
            pixel_pointer = pixel_pointer + width_minus_rwidth
        }

        let pixel_data = get(pixel_pointer)
        let r = pixel_data % 256
        pixel_data = pixel_data / 256
        let g = pixel_data % 256
        pixel_data = pixel_data / 256
        let b = pixel_data % 256

        if !((r == 1) * (g == 1) * (b == 1)) {
            let pixel_x = i % rwidth + x
            let pixel_y = i / rwidth + y
            color(r, g, b)
            point(pixel_x, pixel_y)
        }

        pixel_pointer = pixel_pointer + 1
        i = i + 1
    }
}


fn draw_text(string, font, x, y) {
    let cursor_x_position = x
    let font_size = get(font + 1)  // assume height of the font image is the font size

    let char_pointer = string + 1
    let string_length = get(string)
    let string_end = string + string_length + 1
    while char_pointer < string_end {
        let char_x_position = (get(char_pointer) - 32) * font_size
        draw_texture_region(font, cursor_x_position, y, char_x_position, 0, font_size, font_size)
        cursor_x_position = cursor_x_position + font_size + 1
        char_pointer = char_pointer + 1
    }
}


fn start() {
    let string = "Hello, World!"
    draw_text(string, FONT, 5, 50)
}

fn tick() {
}